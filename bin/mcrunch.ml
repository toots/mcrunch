let error_msgf fmt = Fmt.kstr (fun msg -> Error (`Msg msg)) fmt

let to_underscore = function
  | '.' | '%' | '!' | '?' | ':' | '/' -> true
  | _ -> false

let no_colon str =
  String.exists (function '-' -> true | _ -> false) str |> Bool.not

let filename_to_name filename =
  let tmp = Bytes.create (String.length filename) in
  for idx = 0 to String.length filename - 1 do
    if to_underscore filename.[idx] then Bytes.set tmp idx '_'
    else Bytes.set tmp idx filename.[idx]
  done;
  Bytes.unsafe_to_string tmp

module Sched = Hxd.Make (struct type +'a t = 'a end)

let sched = { Hxd.bind= (fun x fn -> fn (Sched.prj x)); return= Sched.inj }
let lseek = { Hxd.lseek= (fun _ _ _ -> Sched.inj (Ok 0)) }

let pp cfg hs ppf filename =
  let ic = open_in_bin filename in
  let finally () = close_in ic in
  Fun.protect ~finally @@ fun () ->
  let max = in_channel_length ic in
  if max <= 0 then
    match Hxd.is_caml cfg with
    | Some `Array -> Fmt.pf ppf "[||]"
    | Some `List -> Fmt.pf ppf "[]"
    | Some `String -> Fmt.pf ppf "\"\""
    | None -> assert false
  else if max > 0xffff (* 65535 *)
  then
    let pp ppf () =
      let recv (ic, pos) buf ~off ~len =
        let len = Int.min (max - !pos) len in
        let len = input ic buf off len in
        pos := !pos + len;
        List.iter (fun h -> h#feed_bytes ~off ~len buf) hs;
        Sched.inj (Ok len) in
      let send _ _ ~off:_ ~len = Sched.inj (Ok len) in
      let seek = `Relative 0 in
      let v = Hxd.generate cfg sched recv send (ic, ref 0) () lseek seek ppf in
      match Sched.prj v with
      | Ok () -> List.iter (fun h -> h#fin) hs
      | Error _ -> List.iter (fun h -> h#fin) hs in
    Fmt.pf ppf "@[<hov>%a@]" pp ()
  else
    let tmp = Bytes.create 0x7ff in
    let buf = Buffer.create max in
    let rec go () =
      match input ic tmp 0 (Bytes.length tmp) with
      | 0 -> Buffer.contents buf
      | len ->
          Buffer.add_subbytes buf tmp 0 len;
          go ()
      | exception End_of_file -> Buffer.contents buf
    in
    let str = go () in
    List.iter (fun h ->
        h#feed_string str;
        h#fin)
      hs;
    Fmt.pf ppf "@[<hov>%a@]" (Hxd_string.pp cfg) str

let protects ~finallies work =
  List.fold_right (fun finally work () ->
      Fun.protect ~finally work)
    finallies work ()

let run _quiet cfg (lookup, filenames) output checksums =
  let ppf_finally_of_filename filename =
    let oc = open_out_bin filename in
    let ppf = Format.formatter_of_out_channel oc in
    let finally () = close_out oc in
    (ppf, finally)
  in
  let ppf, finally =
    match output with
    | None -> (Fmt.stdout, ignore)
    | Some filename -> ppf_finally_of_filename filename
  in
  let finallies, hs =
    List.map (fun (hash, filename) ->
        let (module H : Digestif.S) = Digestif.module_of_hash' (hash :> Digestif.hash') in
        let ppf, finally = ppf_finally_of_filename filename in
        finally,
        (fun name ->
           object
             val mutable ctx = H.init ()
             method feed_string s =
               ctx <- H.feed_string ctx s
             method feed_bytes ~off ~len buf =
               ctx <- H.feed_bytes ctx buf ~off ~len
             method fin =
               let hash = H.get ctx |> H.to_raw_string in
               Fmt.pf ppf "let %s = @[<hov>%a@]\n%!" name (Hxd_string.pp cfg) hash
           end))
      checksums
    |> List.split
  in
  Fun.protect ~finally @@ fun () ->
  protects ~finallies @@ fun () ->
  let fn (filename, name) =
    let hs = List.map (fun h -> h name) hs in
    Fmt.pf ppf "let %s = @[<hov>%a@]\n%!" name (pp cfg hs) filename;
  in
  List.iter fn filenames;
  match lookup with
  | None -> ()
  | Some lookup ->
      Fmt.pf ppf "\nlet %s = function\n" lookup;
      List.iter
        (fun (filename, name) ->
          Fmt.pf ppf "  | %S -> Some %s\n" filename name)
        filenames;
      Fmt.pf ppf "  | _ -> None\n%!"

let existing_filename filename =
  if Sys.is_regular_file filename then Ok ()
  else error_msgf "%s does not exist" filename

let non_existing_filename filename =
  if Sys.file_exists filename then error_msgf "%s already exists" filename
  else Ok ()

let safe_filename_as_name filename =
  let fn0 = function 'a' .. 'z' | '_' -> true | _ -> false in
  let fn1 = function
    | 'A' .. 'Z' | '0' .. '9' | '\'' -> true
    | chr -> fn0 chr || to_underscore chr
  in
  if
    String.length filename > 0
    && fn0 filename.[0]
    && String.for_all fn1 filename
  then Ok ()
  else error_msgf "%s is not a safe filename" filename

let has_extension exts filename =
  match exts with
  | [] -> true
  | exts ->
      let extension = Filename.extension filename in
      List.exists (fun ext -> String.equal ext extension) exts

(* Entries are sorted so that the generated module only depends on the contents
   of the directory, not on the order the file-system happens to return it in. *)
let rec fold_directory exts fn acc directory =
  let entries = Sys.readdir directory in
  Array.sort String.compare entries;
  Array.fold_left
    (fun acc entry ->
      let filename = Filename.concat directory entry in
      if Sys.is_directory filename then fold_directory exts fn acc filename
      else if Sys.is_regular_file filename && has_extension exts filename then
        fn acc filename
      else acc)
    acc entries

let filenames_of_directory exts directory =
  fold_directory exts (fun acc filename -> (filename, None) :: acc) [] directory
  |> List.rev

let safe_name name =
  let fn0 = function 'a' .. 'z' | '_' -> true | _ -> false in
  let fn1 = function
    | 'A' .. 'Z' | '0' .. '9' | '\'' -> true
    | chr -> fn0 chr
  in
  if String.length name > 0 && fn0 name.[0] && String.for_all fn1 name then
    Ok ()
  else error_msgf "%s is not a safe name" name

open Cmdliner

let parser_of_arg str =
  let ( let* ) = Result.bind in
  match String.split_on_char ':' str with
  | [] -> assert false
  | [ filename ] ->
      let* () = existing_filename filename in
      Ok (filename, None)
  | "-" :: filename ->
      let filename = String.concat ":" filename in
      let* () = existing_filename filename in
      Ok (filename, None)
  | name :: filename ->
      let filename = String.concat ":" filename in
      let* () = existing_filename filename in
      let* () = safe_name name in
      Ok (filename, Some name)

let pp_of_arg ppf = function
  | filename, None ->
      if no_colon filename then Fmt.string ppf filename
      else Fmt.pf ppf "-:%s" filename
  | filename, Some name -> Fmt.pf ppf "%s:%s" name filename

(* A file given without an explicit name is reached through the binding named
   after it, so its filename has to be usable as an OCaml identifier.
   With a lookup function, the contents are reached by filename instead, so we
   are free to name the bindings ourselves and any filename will do. *)
let resolve_name lookup idx (filename, name) =
  let ( let* ) = Result.bind in
  match (name, lookup) with
  | Some name, _ -> Ok (filename, name)
  | None, Some _ -> Ok (filename, Fmt.str "d_%d" idx)
  | None, None ->
      let* () = safe_filename_as_name filename in
      Ok (filename, filename_to_name filename)

let setup_filenames lookup filenames directories exts =
  let ( let* ) = Result.bind in
  let filenames =
    filenames
    @ List.concat_map (filenames_of_directory exts) directories
  in
  let* filenames =
    List.fold_left
      (fun acc filename ->
        let* acc = acc in
        let* filename = resolve_name lookup (List.length acc) filename in
        Ok (filename :: acc))
      (Ok []) filenames
    |> Result.map List.rev
  in
  let rec has_duplicate = function
    | [] -> false
    | x :: r -> List.mem x r || has_duplicate r
  in
  if has_duplicate (List.map snd filenames) then
    error_msgf "Found some duplications on names"
  else if Option.is_some lookup && has_duplicate (List.map fst filenames) then
    error_msgf "Found some duplications on filenames"
  else if List.is_empty filenames then error_msgf "No file specified to crunch"
  else Ok (lookup, filenames)

let filenames =
  let doc =
    "The file to $(i,crunch) into the OCaml output file. The user can specify \
     the OCaml name to obtain the contents of the file using the separator \
     $(i,:) (with the name on the left and the file on the right). If the \
     filename contains the character $(i,:) and the user would like to let \
     $(tname) infer the OCaml name, the user can specify $(i,-:filename)."
  in
  let open Arg in
  value
  & opt_all (conv (parser_of_arg, pp_of_arg)) []
  & info [ "f"; "file" ] ~doc ~docv:"[NAME|-:]FILENAME"

let directories =
  let doc =
    "A directory to $(i,crunch) into the OCaml output file. It is walked \
     recursively and every regular file found is crunched as if it was given \
     with $(b,--file), using the path $(tname) walked to it (including \
     $(i,DIRECTORY) itself) to infer the OCaml name. This option can be \
     repeated."
  in
  let parser directory =
    if Sys.file_exists directory && Sys.is_directory directory then Ok directory
    else error_msgf "%s is not a directory" directory
  in
  let open Arg in
  value
  & opt_all (conv (parser, Fmt.string)) []
  & info [ "d"; "directory" ] ~doc ~docv:"DIRECTORY"

let exts =
  let doc =
    "Only crunch the files with this extension when walking a $(b,--directory). \
     The leading $(i,.) is optional. If the option is not given, every file is \
     crunched. This option can be repeated."
  in
  let parser ext =
    if ext <> "" && ext.[0] = '.' then Ok ext else Ok ("." ^ ext)
  in
  let open Arg in
  value
  & opt_all (conv (parser, Fmt.string)) []
  & info [ "e"; "ext" ] ~doc ~docv:"EXTENSION"

let lookup =
  let doc =
    "Also emit a function mapping each crunched filename to its contents, so \
     that they can be reached by name at run-time instead of through the \
     bindings $(tname) infers. The function is called $(i,read) unless \
     $(i,NAME) says otherwise, and returns an $(i,option). In this mode \
     $(tname) names the bindings of the files given without an explicit name \
     itself, which lifts the restriction that such a filename must be usable \
     as an OCaml identifier."
  in
  let ( let* ) = Result.bind in
  let parser name =
    let* () = safe_name name in
    Ok name
  in
  let open Arg in
  value
  & opt ~vopt:(Some "read") (some (conv (parser, Fmt.string))) None
  & info [ "lookup" ] ~doc ~docv:"NAME"

let setup_filenames =
  let open Term in
  term_result ~usage:false
    (const setup_filenames $ lookup $ filenames $ directories $ exts)

let output_options = "OUTPUT OPTIONS"

let verbosity =
  let env = Cmd.Env.info "CRUNCH_LOGS" in
  Logs_cli.level ~docs:output_options ~env ()

let renderer =
  let env = Cmd.Env.info "CRUNCH_FMT" in
  Fmt_cli.style_renderer ~docs:output_options ~env ()

let utf_8 =
  let doc = "Allow binaries to emit UTF-8 characters." in
  let env = Cmd.Env.info "CRUNCH_UTF_8" in
  Arg.(value & opt bool true & info [ "with-utf-8" ] ~doc ~env)

let reporter ppf =
  let report src level ~over k msgf =
    let k _ =
      over ();
      k ()
    in
    let with_metadata header _tags k ppf fmt =
      Fmt.kpf k ppf
        ("[%a]%a[%a]: " ^^ fmt ^^ "\n%!")
        Fmt.(styled `Cyan int)
        (Stdlib.Domain.self () :> int)
        Logs_fmt.pp_header (level, header)
        Fmt.(styled `Magenta string)
        (Logs.Src.name src)
    in
    msgf @@ fun ?header ?tags fmt -> with_metadata header tags k ppf fmt
  in
  { Logs.report }

let setup_logs utf_8 style_renderer level =
  Fmt_tty.setup_std_outputs ~utf_8 ?style_renderer ();
  Logs.set_level level;
  Logs.set_reporter (reporter Fmt.stderr);
  Option.is_none level

let setup_logs = Term.(const setup_logs $ utf_8 $ renderer $ verbosity)
let docs_hexdump = "HEX OUTPUT"

let with_comments =
  let doc =
    "Print a human-readable view of each line of the contents as a comment. \
     Not supported for string output."
  in
  let open Arg in
  value & flag & info [ "with-comments" ] ~doc ~docs:docs_hexdump

let cols =
  let doc = "Format $(i,COLS) octets per line. Default 16. Max 256." in
  let parser str =
    match int_of_string str with
    | n when n < 1 || n > 256 ->
        error_msgf "Invalid COLS value (must <= 256 && > 0): %d" n
    | n -> Ok n
    | exception _ -> error_msgf "Invalid COLS value: %S" str
  in
  let open Arg in
  let cols = conv (parser, Fmt.int) in
  value
  & opt (some cols) None
  & info [ "c"; "cols" ] ~doc ~docv:"COLS" ~docs:docs_hexdump

let kind =
  let open Arg in
  let array =
    info [ "a"; "array" ] ~doc:"Serialize the contents to an array of strings."
  in
  let list =
    info [ "l"; "list" ] ~doc:"Serialize the contents to a list of strings."
  in
  let string =
    info [ "s"; "string" ] ~doc:"Serialize the contents to a single string."
  in
  value & vflag `Array [ (`Array, array); (`List, list) ; (`String, string) ; ]

let uppercase =
  let doc = "Use upper case hex letters. Default is lower case." in
  let open Arg in
  value & flag & info [ "u" ] ~doc ~docs:docs_hexdump

let setup_hxd with_comments cols uppercase kind =
  match kind with
  | `Array | `List as kind -> Hxd.caml ~with_comments ?cols ~uppercase kind
  | `String ->
    if with_comments then
      Printf.eprintf "Comments are not supported for string output. \
                      Not outputting comments.\n%!";
    Hxd.caml_string ?cols ~uppercase ()

let setup_hxd =
  let open Term in
  const setup_hxd $ with_comments $ cols $ uppercase $ kind

let output =
  let ( let* ) = Result.bind in
  let doc = "The OCaml output file." in
  let parser = function
    | "-" -> Ok None
    | filename ->
        let* _ = Fpath.of_string filename in
        let* () = non_existing_filename filename in
        Ok (Some filename)
  in
  let pp ppf = function
    | None -> Fmt.string ppf "-"
    | Some filename -> Fmt.string ppf filename
  in
  let open Arg in
  value
  & opt (conv (parser, pp)) None
  & info [ "o"; "output" ] ~doc ~docv:"FILENAME"

let checksums =
  let doc = "Output OCaml code containing checksums for the files. \
             The format is [<hash>:]destination.ml. \
             The default hash is SHA256." in
  let parser filename =
    let ( let* ) = Result.bind in
    match String.index_opt filename ':' with
    | None ->
      let* _ = Fpath.of_string filename in
      Ok (`SHA256, filename)
    | Some idx ->
      let* hash =
        let hash = String.sub filename 0 idx in
        match String.lowercase_ascii hash with
        | "md5" -> Ok `MD5
        | "sha1" -> Ok `SHA1
        | "sha224" -> Ok `SHA224
        | "sha256" -> Ok `SHA256
        | "sha384" -> Ok `SHA384
        | "sha512" -> Ok `SHA512
        | _ -> Error (`Msg ("Unknown hash: " ^ hash))
      in
      let filename = String.sub filename (succ idx) (String.length filename - succ idx) in
      let* _ = Fpath.of_string filename in
      Ok (hash, filename)
  and pp ppf (hash, filename) =
    let pp_hash ppf = function
      | `MD5 -> Fmt.string ppf "md5"
      | `SHA1 -> Fmt.string ppf "sha1"
      | `SHA224 -> Fmt.string ppf "sha224"
      | `SHA256 -> Fmt.string ppf "sha256"
      | `SHA384 -> Fmt.string ppf "sha384"
      | `SHA512 -> Fmt.string ppf "sha512"
    in
    Fmt.pf ppf "%a:%s" pp_hash hash filename
  in
  Arg.(value & opt_all (conv (parser, pp)) [] & info [ "checksums" ] ~doc ~docv:"FILENAME")

let term =
  let open Term in
  const run $ setup_logs $ setup_hxd $ setup_filenames $ output $ checksums

let cmd =
  let doc =
    "Crunch some files into an OCaml file which can be statically linked with \
     an OCaml program. The OCaml program is able to obtain the contents of \
     these files then without I/O operations."
  in
  let info = Cmd.info "mcrunch" ~doc in
  Cmd.v info term

let () = Cmd.(exit @@ eval cmd)
